<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <title>defer shading</title>
</head>
<body>
<canvas id="gl-canvas"></canvas>
<script id="geo-vs" type="x-vertex-shader">
        #version 300 es

        layout(std140, column_major) uniform;

        layout(location=0) in vec4 aPosition;
        layout(location=1) in vec3 aNormal;
        layout(location=2) in vec4 aUV;

        uniform Matrices {
            mat4 uModelMatrix;
            mat4 uMVP;
        };

        out vec4 vPosition;
        out vec4 vNormal;
        out vec4 vUV;

        void main() {
            vPosition = uModelMatrix * aPosition;
            vNormal = uModelMatrix * vec4(aNormal, 0.0);
            vUV = aUV;
            gl_Position = uMVP * aPosition;
        }
    </script>
<script id="geo-fs" type="x-fragment-shader">
        #version 300 es
        precision highp float;

        in vec4 vPosition;
        in vec4 vNormal;
        in vec4 vUV;

        layout(location=0) out vec4 fragPosition;
        layout(location=1) out vec4 fragNormal;
        layout(location=2) out vec4 fragUV;

        void main() {
            fragPosition = vPosition;
            fragNormal = vec4(normalize(vNormal.xyz), 0.0);
            fragUV = vUV;
        }
    </script>
<script id="main-vs" type="x-vertex-shader">
        #version 300 es

        layout(std140, column_major) uniform;

        layout(location=0) in vec4 aPosition;

        uniform LightUniforms {
            vec4 position;
            vec4 color;
        } uLight;

        void main() {
            //gl_Position = uLight.mvp * aPosition;
            //render full screen quad(ndr)
            gl_Position = aPosition;
        }
    </script>
<script id="main-fs" type="x-fragment-shader">
        #version 300 es
        precision highp float;

        uniform LightUniforms {
            vec4 position;
            vec4 color;
        } uLight;

        uniform vec3 uEyePosition;

        uniform sampler2D uPositionBuffer;
        uniform sampler2D uNormalBuffer;
        uniform sampler2D uUVBuffer;
        uniform sampler2D uTextureMap;

        out vec4 fragColor;

        void main() {
            ivec2 fragCoord = ivec2(gl_FragCoord.xy);
            vec3 position = texelFetch(uPositionBuffer, fragCoord, 0).xyz;
            vec3 normal = normalize(texelFetch(uNormalBuffer, fragCoord, 0).xyz);
            vec2 uv = texelFetch(uUVBuffer, fragCoord, 0).xy;

            vec4 baseColor = texture(uTextureMap, uv);

            vec3 eyeDirection = normalize(uEyePosition - position);
            vec3 lightVec = uLight.position.xyz - position;
            float attenuation = 1.0 - length(lightVec);
            vec3 lightDirection = normalize(lightVec);
            vec3 reflectionDirection = reflect(-lightDirection, normal);
            float nDotL = max(dot(lightDirection, normal), 0.0);
            vec3 diffuse = nDotL * uLight.color.rgb;
            float ambient = 0.1;
            vec3 specular = pow(max(dot(reflectionDirection, eyeDirection), 0.0), 20.0) * uLight.color.rgb;

            fragColor = vec4(attenuation * (ambient + diffuse + specular) * baseColor.rgb, baseColor.a);
        }
    </script>

<script src="utils/gl-matrix.js"></script>
<script src="utils/utils.js"></script>

<script src="defer.js"></script>
</body>
</html>